import java.io.BufferedWriter;
import java.io.FileWriter;
import java.io.IOException;
import java_cup.runtime.Symbol;



parser code {: 
    private Lexer scanner;
    private SymbolTable symbolTable;
    private BufferedWriter syntaxFile;
    private BufferedWriter semanticFile;


    @SuppressWarnings("deprecation")
    public Parser(Lexer scanner) {
        this.scanner = scanner;
        this.symbolTable = new SymbolTable();
    }

    public void createSemanticWriter(String root) throws IOException {
        semanticFile = new BufferedWriter(new FileWriter(root));
    }


    public void createSyntaxWriter(String root) throws IOException {
        syntaxFile = new BufferedWriter(new FileWriter(root));
    }


    public void reportSyntaxError(int line, int column) throws IOException {
        if(syntaxFile != null) {
            syntaxFile.write("Error sintáctico recuperado en línea " + line + ", columna " + column + ".\n");
            syntaxFile.flush();
        }
    }


    public void reportSemanticError(String text) throws IOException {
        if(semanticFile != null) {
            semanticFile.write(text);
            semanticFile.flush();
        }
    }


    public void closeSyntaxWriter() throws IOException {
        if(syntaxFile != null) {
            syntaxFile.close();
        }
    }


    public void closeSemanticWriter() throws IOException {
        if(semanticFile != null) {
            semanticFile.close();
        }
    }


    @Override
    public void syntax_error(Symbol current) {
        try {
          reportSyntaxError(current.left, current.right);
        } catch (IOException e) {
          e.printStackTrace();
        }
    }

:};



scan with {: return scanner.next_token(); :};

//tipos de datos
terminal INTEGER, FLOAT, BOOL, CHAR, STRING, INT_LITERAL, STRING_LITERAL, BOOL_LITERAL, FLOAT_LITERAL, CHAR_LITERAL;

//bloques de código
terminal APERTURA_DE_BLOQUE, CIERRE_DE_BLOQUE;

//corchetes
terminal CORCHETE_APERTURA, CORCHETE_CIERRE;

//asignacion =
terminal ASIGNACION;

//Parentesis
terminal PARENTESIS_APERTURA, PARENTESIS_CIERRE; 

//expresiones aritmeticas
terminal SUMA, RESTA, MULTIPLICACION, MODULO, POTENCIA, DIVISION;

//expresiones aritmeticas unarias
terminal INCREMENTO, DECREMENTO;

//expresiones relacionales (igualdad es ==)
terminal MENOR, MENOR_IGUAL, MAYOR, MAYOR_IGUAL, IGUALDAD, DIFERENTE;

//expresiones logicas
terminal CONJUNCION, DISYUNCION, NEGACION;

//estructuras de control
terminal IF, ELSE, WHILE, FOR, SWITCH, CASE, DEFAULT, BREAK, RETURN, DOS_PUNTOS, COMMA;

//funciones de leer
terminal PRINT, READ;

//identificadores
terminal IDENTIFICADOR;

//final de expresion y Main
terminal END_EXPR, MAIN;

non terminal tipo, literal, op_aritmeticas, dato_general, sec_oper, imprimir, leer_var, inicializacion, 
actualizacion, asignacion_arreglo, asignacion_indice, asignacion_variable, asignacion_compuesta, 
declaracion_arreglo_op, declaracion_compuesta, op_unaria, finfuncion, nombrefuncion, parametro_decl, parametros_decl, else_expr, else_op, relacion, 
comparacion_operacion, op_logica, expr_logica_aritmetica, if_expr, cases, switch_expr, op_simple, simple_expr, while_expr, for_expr, 
bloque, bloques, funciones, funcion, programa, llamada_funcion, parametros_llamada, expr_unaria, acceso_arreglo, default_op, declaracion_variable_asignacion;


//precedencias
precedence left SUMA, RESTA;
precedence left MULTIPLICACION, DIVISION, MODULO;
precedence right POTENCIA;
precedence left DISYUNCION;
precedence left CONJUNCION;
precedence right NEGACION;
precedence left MENOR, MENOR_IGUAL, MAYOR, MAYOR_IGUAL, IGUALDAD, DIFERENTE;
precedence right INCREMENTO, DECREMENTO;

start with programa;



//Reglas gramáticales


// Regla para la declaración de parámetros en una función.
parametro_decl ::= tipo:t IDENTIFICADOR:idVar COMMA parametro_decl:param 
{: 
    // Intenta agregar el parámetro a la tabla de símbolos.
    // Si ya existe, reporta un error semántico.
    if(!symbolTable.addSymbol(idVar.toString(), t + ":" + idVar)) {
        reportSemanticError("Error semántico en la declaración de una función, hay parámetros repetidos [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
    } 
    RESULT = t + ":"  + param; 
:}
| tipo:t IDENTIFICADOR:idVar 
{: 
    // Similar al caso anterior, pero para el último parámetro de la lista.
    if(!symbolTable.addSymbol(idVar.toString(), t + ":" + idVar)) {
        reportSemanticError("Error semántico en la declaración de una función, hay parámetros repetidos [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
    } 
    RESULT = t.toString(); 
:};

// Regla para definir el conjunto completo de parámetros en una función.
parametros_decl ::= parametro_decl:params {: RESULT = params; :} | {: RESULT = ""; :};


// Regla para las operaciones en las llamadas a funciones.
sec_oper ::= expr_logica_aritmetica:dato COMMA sec_oper:seq {: RESULT = dato + ":" + seq; :} | expr_logica_aritmetica:dato {: RESULT = dato; :};


// Definición de los parámetros en las llamadas a funciones.
parametros_llamada ::= sec_oper:seq {: RESULT = seq; :} | {: RESULT = ""; :} ;


// Regla para las llamadas a funciones.
llamada_funcion ::= nombrefuncion:f PARENTESIS_APERTURA parametros_llamada:params PARENTESIS_CIERRE 
{:
    String funcName = f.toString();
    // Recupera el tipo de la función de la tabla de símbolos.
    RESULT = symbolTable.getFunctionType(funcName); 
    // Verifica que la llamada sea válida en términos de parámetros y existencia.
    if(!symbolTable.verifyFunctionCall(funcName, params.toString())) {
        reportSemanticError("Error en el tipo de datos de una llamada a una función o existencia de una variable o función fuera del scope o cantidad incorrecta de parámetros [Fila: " + fleft + ", Columna: " + fright + "].\n");
    }
:};


// Definición de tipos básicos.
tipo ::= INTEGER {: RESULT = "int"; :}
        | FLOAT {: RESULT = "float"; :}
        | BOOL {: RESULT = "bool"; :}
        | CHAR {: RESULT = "char"; :}
        | STRING {: RESULT = "string"; :};


// Reglas para literales de diferentes tipos.
literal ::= INT_LITERAL:intVar {: RESULT = "int"; /* intVar.toString(); */ :}
          | STRING_LITERAL:strVar {: RESULT = "string"; /* strVar.toString(); */ :}
          | FLOAT_LITERAL:floatVar {: RESULT = "float"; /* floatVar.toString(); */ :}
          | CHAR_LITERAL:charVar {:  RESULT = "char"; /* charVar.toString(); */ :}
          | BOOL_LITERAL:boolVar {:  RESULT = "bool"; /* boolVar.toString(); */ :};

// Operadores aritméticos básicos.
op_aritmeticas ::= SUMA {: RESULT = "+"; :} 
                | RESTA {: RESULT = "-"; :} 
                | MULTIPLICACION {: RESULT = "*"; :} 
                | DIVISION {:  RESULT = "/"; :} 
                | MODULO {: RESULT = "%"; :} 
                | POTENCIA {:  RESULT = "^"; :};


// Regla para datos generales: identificadores, literales, funciones o arreglos.
dato_general ::= IDENTIFICADOR:idVar {: RESULT = idVar.toString(); :} 
                | literal:lit {: RESULT = lit; :} 
                | llamada_funcion:func {: RESULT = func; :} 
                | acceso_arreglo:arrName {: RESULT = arrName; :};


// Operadores unarios como incremento y decremento.
op_unaria ::= INCREMENTO {:  RESULT = "++"; :} | DECREMENTO {:  RESULT = "--"; :};


// Expresiones unarias, aplicadas sobre identificadores.
expr_unaria ::= IDENTIFICADOR:idVar op_unaria 
{:  
    String var = idVar.toString();
    RESULT = var; 
    if(!symbolTable.isInLocalScope(var)) {
        reportSemanticError("Error semántico, identificador fuera del scope [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
    }
:};



// Simplificación para imprimir un único valor.
imprimir ::= PRINT PARENTESIS_APERTURA expr_logica_aritmetica PARENTESIS_CIERRE; //A petición del profe, se simplifica a un solo valor.
// Leer un valor desde la entrada del usuario.
leer_var ::= READ PARENTESIS_APERTURA IDENTIFICADOR:i 
{:
    if(!symbolTable.isInLocalScope(i.toString())) {
        reportSemanticError("Error semántico, la variable que se intenta leer no ha sido declarada dentro del scope [Fila: " + ileft + ", Columna: " + iright + "].\n");
    }
:} PARENTESIS_CIERRE; 


// Leer un valor desde la entrada del usuario.
acceso_arreglo ::= IDENTIFICADOR:idVar CORCHETE_APERTURA expr_logica_aritmetica:e CORCHETE_CIERRE 
{: 
    if(!symbolTable.isIndexInteger(e.toString())) {
        reportSemanticError("Error semántico, el tipo de dato de un índice debe ser entero [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
    }
    RESULT = idVar.toString(); 
:};




// Regla para actualización de valores en asignaciones.
actualizacion ::= ASIGNACION {: RESULT = "="; :} | op_aritmeticas:arit_op ASIGNACION {: RESULT = arit_op + "="; :};
// Regla para actualización de valores en asignaciones.
asignacion_arreglo ::= IDENTIFICADOR:idVar ASIGNACION APERTURA_DE_BLOQUE sec_oper:seq CIERRE_DE_BLOQUE 
{:
    RESULT = idVar + ":" + seq;
    if(!symbolTable.verifyArrayDeclaration(symbolTable.getType(idVar.toString()), seq.toString())) { 
        reportSemanticError("Error en el tipo de datos de una asignación o hay una variable que está fuera del scope [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
    }

:};


// Regla para actualización de valores en asignaciones.
asignacion_indice ::= acceso_arreglo:arr actualizacion:act expr_logica_aritmetica:e 
{:
    String arrName = arr.toString();
    RESULT = arrName;                         
    if(!symbolTable.verifyType(arrName, e.toString())) {
        reportSemanticError("Error en el tipo de datos de una asignación o hay una variable que está fuera del scope [Fila: " + arrleft + ", Columna: " + arrright + "].\n");
    }
:};



// Asignación de valores a una variable.
asignacion_variable ::= IDENTIFICADOR:idVar  actualizacion:act  expr_logica_aritmetica:e 
{: 
    RESULT = idVar + ":" + act + ":" + e.toString();
    if(!symbolTable.verifyType(idVar.toString(), e.toString())) {
        reportSemanticError("Error en el tipo de datos de una asignación o hay una variable que está fuera del scope [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
    }
:};


// Regla general para asignaciones: puede ser a una variable, arreglo o un índice de arreglo.
asignacion_compuesta ::= asignacion_variable | asignacion_arreglo | asignacion_indice;



// Declaración opcional de un arreglo, con o sin asignación de valores iniciales.
declaracion_arreglo_op ::= ASIGNACION APERTURA_DE_BLOQUE sec_oper:seq CIERRE_DE_BLOQUE {: RESULT = seq.toString(); :} | {: RESULT = ""; :};



// Declaración de una variable normal con asignación
declaracion_variable_asignacion ::= tipo:t IDENTIFICADOR:idVar ASIGNACION expr_logica_aritmetica:e 
    {:     
        String type = t.toString();
        RESULT = type; 
        if(!symbolTable.addSymbol(idVar.toString(), type + ":" + idVar)) {
            reportSemanticError("Error semántico en una declaración. Ya existe una variable con el mismo nombre dentro del scope [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
        } 
        if(!symbolTable.verifyType(type, e.toString())) {
            reportSemanticError("Error en el tipo de datos en la asignación de una declaración o existencia de una variable fuera del scope [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
        }
    :};



// Declaración de variables, con validaciones semánticas.
declaracion_compuesta ::= tipo:t IDENTIFICADOR:idVar 
              {: 
                String type = t.toString(); 
                RESULT = type; 
                if(!symbolTable.addSymbol(idVar.toString(), type + ":" + idVar)) {
                    reportSemanticError("Error semántico en una declaración. Ya existe una variable con el mismo nombre dentro del scope [Fila: " + idVarleft + ", Columna: " + idVarright + "].\n");
                } 
              :}
              | declaracion_variable_asignacion:t {: RESULT = t; :} | tipo:t acceso_arreglo:arrName declaracion_arreglo_op:arr_decl 
              {:    
                    String text = t.toString();  
                    RESULT = text; 
                    if(!symbolTable.addSymbol(arrName.toString(), text + ":" + arrName)) {
                        reportSemanticError("Error semántico en una declaración. Ya existe una variable con el mismo nombre dentro del scope [Fila: " + arrNameleft + ", Columna: " + arrNameright + "].\n");
                    }  
                    if(!symbolTable.verifyArrayDeclaration(t.toString(), arr_decl.toString())) {
                        reportSemanticError("Error en el tipo de datos en la asignación de una declaración o existencia de una variable fuera del scope [Fila: " + arrNameleft + ", Columna: " + arrNameright + "].\n");
                    }
              :};





// Definición de una cláusula else opcional.
else_expr ::= ELSE APERTURA_DE_BLOQUE {: symbolTable.addScope(); :}  bloques CIERRE_DE_BLOQUE {: symbolTable.printScope(); symbolTable.exitScope(); :}; 

// Definición del operador else como opcional.
else_op ::= else_expr | {: RESULT = ""; :};


// Relación entre dos expresiones (comparaciones).
relacion ::= MENOR {: RESULT = "<"; :} 
            | MENOR_IGUAL {: RESULT = "<="; :} 
            | MAYOR {: RESULT = ">"; :} 
            | MAYOR_IGUAL {: RESULT = ">="; :} 
            | IGUALDAD {: RESULT = "=="; :} 
            | DIFERENTE {: RESULT = "!="; :};


// Comparaciones y operaciones aritméticas.
comparacion_operacion ::= expr_logica_aritmetica:e1  relacion:rel  expr_logica_aritmetica:e2 
{: 
    // Valida la operación y su compatibilidad semántica.
    if(!symbolTable.validateOperation(e1.toString(), rel.toString(), e2.toString())) {
        reportSemanticError("Error semántico al operar o comparar tipos que no son compatibles o están fuera del scope [Fila: " + e1left + ", Columna: " + e2right + "].\n");
    }
    RESULT = e1.toString(); 
:} | expr_logica_aritmetica:e1  op_aritmeticas:op  expr_logica_aritmetica:e2 
{: 
    if(!symbolTable.validateOperation(e1.toString(), op.toString(), e2.toString())) {
        reportSemanticError("Error semántico al operar o comparar tipos que no son compatibles o están fuera del scope [Fila: " + e1left + ", Columna: " + e1right + "].\n");
    }
    RESULT = e1.toString(); 

:} 
| dato_general:dato 
{: 
    String operando = dato.toString();
    RESULT = operando;
    if(!symbolTable.isDataType(operando) && !symbolTable.isInLocalScope(operando)) {
        reportSemanticError("Error semantico, variable fuera del scope [Fila: " + datoleft + ", Columna: " + datoright + "].\n");
    }

:} | expr_unaria:op {: RESULT = op; :}; 



// Operadores lógicos  and y or  
op_logica ::= CONJUNCION {: RESULT = "and"; :} | DISYUNCION {: RESULT = "or"; :};



// Expresiones lógicas y aritméticas con validaciones.
expr_logica_aritmetica ::= NEGACION expr_logica_aritmetica:e 
{: 
    RESULT = e.toString(); 
:} 
| expr_logica_aritmetica:e1  op_logica:op  expr_logica_aritmetica:e2 
{: 
    /* Aqui no verifico porque se trata de operaciones logicas como and, or o not */
    RESULT = e1.toString();  
:} 
| comparacion_operacion:cmp 
{: 
    RESULT = cmp; 
:} 
| PARENTESIS_APERTURA expr_logica_aritmetica:e PARENTESIS_CIERRE 
{: 
    RESULT = e.toString(); 
:}; 



// Expresiones lógicas y aritméticas con validaciones.
if_expr ::= IF PARENTESIS_APERTURA expr_logica_aritmetica PARENTESIS_CIERRE APERTURA_DE_BLOQUE  {: symbolTable.addScope(); :}   bloques   CIERRE_DE_BLOQUE  {: symbolTable.printScope(); symbolTable.exitScope(); :}  else_op
| IF error {: System.out.println("Error sintáctico en la definición de un if."); :} APERTURA_DE_BLOQUE {: symbolTable.addScope(); :} bloques CIERRE_DE_BLOQUE  {: symbolTable.printScope(); symbolTable.exitScope(); :}  else_op;


// Definición de un caso por defecto en una estructura SWITCH.
default_op ::= DEFAULT DOS_PUNTOS bloques | ;

// Definición de casos en una estructura SWITCH
cases ::= CASE literal DOS_PUNTOS bloques cases | CASE literal DOS_PUNTOS bloques;

// Definición de una estructura SWITCH con casos y un DEFAULT opcional.
switch_expr ::= SWITCH PARENTESIS_APERTURA dato_general PARENTESIS_CIERRE APERTURA_DE_BLOQUE  {: symbolTable.addScope(); :}  cases   default_op   CIERRE_DE_BLOQUE {: symbolTable.printScope(); symbolTable.exitScope(); :}
| SWITCH error {: System.out.println("Error sintáctico en la definición de una switch."); :} APERTURA_DE_BLOQUE  {: symbolTable.addScope(); :}  cases   default_op   CIERRE_DE_BLOQUE {: symbolTable.printScope(); symbolTable.exitScope(); :};



// Definición de la expresión RETURN dentro de funciones, con validación de tipos
finfuncion ::= RETURN expr_logica_aritmetica:dato 
{:
     // Verifica que el tipo de retorno sea compatible con el tipo declarado de la función.
    if(!symbolTable.verifyType(dato.toString(), symbolTable.getFunctionType(symbolTable.getActualFunction()))) {
        reportSemanticError("Error semántico en el tipo de retorno de función [Fila: " + datoleft + ", Columna: " + datoright + "].\n");
    } 
:} | RETURN:ret {: reportSemanticError("Error semántico, el valor de retorno no coincide con el tipo de la función [Fila: " + retleft + ", Columna: " + retright + "].\n"); :};



// Operaciones simples que pueden ser expresiones unarias o asignaciones.
op_simple ::= expr_unaria | asignacion_variable;

// Expresiones simples, como declaraciones, asignaciones, lectura/escritura, o llamadas a funciones.
simple_expr ::= expr_unaria | declaracion_compuesta | asignacion_compuesta | leer_var | imprimir | BREAK | llamada_funcion | finfuncion | error {: RESULT = "Error"; System.out.println("Error sintáctico en la definición de una expresión simple."); :};

// Inicialización de variables, que puede ser una asignación o declaración.
inicializacion ::= asignacion_variable:asign {: RESULT = asign; :} | IDENTIFICADOR:i 
{: 

    RESULT = i; 
    if(!symbolTable.isInLocalScope(i.toString())) {
        reportSemanticError("Error semántico, identificador fuera del scope [Fila: " + ileft + ", Columna: " + iright + "].\n");
    }
:} | declaracion_variable_asignacion:dec {: RESULT = dec; :};



// Estructura de control WHILE, con validación de bloques y manejo de scopes.
while_expr ::= WHILE PARENTESIS_APERTURA  expr_logica_aritmetica  PARENTESIS_CIERRE APERTURA_DE_BLOQUE  {: symbolTable.addScope(); :}  bloques  CIERRE_DE_BLOQUE {: symbolTable.printScope(); symbolTable.exitScope(); :} 
| WHILE error {: System.out.println("Error sintáctico en la definición de un while."); :} APERTURA_DE_BLOQUE  {: symbolTable.addScope(); :}  bloques  CIERRE_DE_BLOQUE {: symbolTable.printScope(); symbolTable.exitScope(); :};

// Estructura de control FOR con inicialización, condición, y actualización.
for_expr ::= FOR PARENTESIS_APERTURA  {: symbolTable.addScope(); :}  inicializacion  COMMA  expr_logica_aritmetica  COMMA  op_simple  PARENTESIS_CIERRE APERTURA_DE_BLOQUE   bloques  CIERRE_DE_BLOQUE {: symbolTable.printScope(); symbolTable.exitScope(); :}
| FOR error {: System.out.println("Error sintáctico en la definición de un for."); :} APERTURA_DE_BLOQUE  {: symbolTable.addScope(); :}  bloques  CIERRE_DE_BLOQUE {: symbolTable.printScope(); symbolTable.exitScope(); :};



// Un único bloque de código puede ser un IF, WHILE, FOR, SWITCH, o una expresión simple.
bloque ::= if_expr | while_expr | for_expr | switch_expr | simple_expr END_EXPR; 

// Secuencia de bloques de código (uno tras otro).
bloques ::= bloque bloques | error {: System.out.println("Error dentro de bloque, verificar definición."); RESULT = "Error"; :} | ; 


// Definición de nombres de funciones, incluyendo el nombre especial MAIN.
nombrefuncion ::= MAIN {: RESULT = "_verano_"; :} | IDENTIFICADOR:nombreFunc {: RESULT = nombreFunc; :};


// Conjunto de funciones en el programa.
funciones ::= funcion | funcion funciones | error {: System.out.println("Error fatal en la definición de una función, deteniendo parseo."); :}; 


// Definición de una función con su tipo, nombre, parámetros y cuerpo.
funcion ::= tipo:t nombrefuncion:funcName PARENTESIS_APERTURA {: symbolTable.addScope(); symbolTable.setActualFunction(funcName.toString()); :}  parametros_decl:params  PARENTESIS_CIERRE 
{:
    if(!symbolTable.addGlobalSymbol(funcName.toString(), t + ":" + funcName + ":" + params)) {
        reportSemanticError("Error semántico durante declaración, ya existe una función con el mismo nombre [Fila: " + funcNameleft + ", Columna: " + funcNameright + "].\n");
    }
:}  
APERTURA_DE_BLOQUE   bloques  CIERRE_DE_BLOQUE 
{: 
    symbolTable.printScope(); 
    symbolTable.exitScope();
    symbolTable.setActualFunction(""); 
:} | 
tipo:t nombrefuncion:funcName PARENTESIS_APERTURA {: symbolTable.addScope(); symbolTable.setActualFunction(funcName.toString()); :}  error  PARENTESIS_CIERRE 
{:
    if(!symbolTable.addGlobalSymbol(funcName.toString(), t + ":" + funcName + ":error")) {
        reportSemanticError("Error semántico durante declaración, ya existe una función con el mismo nombre [Fila: " + funcNameleft + ", Columna: " + funcNameright + "].\n");
    }
:} 
APERTURA_DE_BLOQUE  bloques  CIERRE_DE_BLOQUE 
{: 
    System.out.println("Error en la definición de parámetros de una función."); 
    symbolTable.printScope(); 
    symbolTable.exitScope(); 
    symbolTable.setActualFunction("");
:};



// Definición del programa principal, compuesto por funciones
programa ::=  funciones {: symbolTable.printGlobalScope(); :} | {: RESULT = ""; :};


/* ejecutar desde Compilador */
/* java -cp lib/java-cup-11b.jar java_cup.Main -parser Parser -symbols sym -destdir src src/parser.cup */
